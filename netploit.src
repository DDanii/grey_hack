if params.len == 0 or params[0] == "-h" or params[0] == "--help" then exit("<b>Usage: " + program_path.split("/")[-1] + " [ip_address|path] [port]</b>")

metaxploit = include_lib("/lib/metaxploit.so")
if not metaxploit then
    metaxploit = include_lib(current_path + "/metaxploit.so")
	if not metaxploit then
		exit("Missing metaxploit.so!")
	end if
end if

metaLib = null

if is_valid_ip(params[0]) then
	address = params[0]
	port = 0
	if params.len > 1 then port = params[1].to_int
	net_session = metaxploit.net_use( address, port )
	
	if not net_session then
		exit("Can't connect!")
	end if

	metaLib = net_session.dump_lib
else
	metaLib = metaxploit.load(params[0])
end if

if not metaLib then exit("No lib found.")

regions = metaxploit.scan(metaLib)
print(metaLib.lib_name + " " + metaLib.version)
print(regions)
for r in regions
	result = metaxploit.scan_address(metaLib, r)
	lines = result.split("\n")
	for line in lines
		if line.indexOf("Unsafe check:") == 0 then
			s = line.indexOf(">") + 1
			e = line.indexOf("<", s)
			value = line[s:e]
			print("netoverflow " + params.join(" ") + " " + r + " " + value)
		else if line.indexOf("*") == 0 then
			print(line)
		end if
	end for
end for